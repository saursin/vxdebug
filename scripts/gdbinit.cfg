################################################################################
# VxDebug GDB Initialization File
################################################################################
# ===== Useful Aliases =====

# Initialize VX Debug environment
define vxgdb_initialize
    set $num_cores   = $vx_num_cores
    set $num_warps   = $vx_num_warps
    set $num_threads = $vx_num_threads
    printf "Platform Info:\n"
    printf "  num_cores   = %d\n", $num_cores
    printf "  num_warps   = %d\n", $num_warps
    printf "  num_threads = %d\n", $num_threads
end

# Switch to a specific thread based on warp and thread id
define vx_thread
    if $argc != 2
        printf "Usage: vx_thread <warp-id> <thread-id>\n"
    else
        set $wid = $arg0
        set $tid = $arg1
        set $g_tid = $wid * $num_threads + $tid
        set $gdbtid = $g_tid + 1
        thread $gdbtid
        printf "Switched to Warp %d, Lane %d (Thread %d)\n", $wid, $tid, $g_tid
    end
end

define vx_warp
    if $argc != 1
        printf "Usage: vx_warp <warp-id>\n"
    else
        set $wid = $arg0
        printf "Warp %d contains lanes:\n", $wid
        set $tid = 0
        while $tid < $num_threads
            printf "  lane %d -> thread %d\n", $tid, $wid * $num_threads + $tid
            set $tid = $tid + 1
        end
    end
end

define vx_info
    set $g_tid = $num_threads * $vx_warp_id + $vx_thread_id
    printf "Current Thread (%d): Core %d, Warp %d, Thread %d\n", $g_tid, $vx_core_id, $vx_warp_id, $vx_thread_id
end



# === Command history ===
set history save on
set history filename .gdb_history
set history size 1000

# === Remote debugging verbosity ===
define verbose_remote
    if $argc != 1
        printf "Usage: verbose_remote <0|1>\n"
    else
        if $arg0 == 1
            set debug remote 1
            printf "Remote debugging verbosity enabled.\n"
        else
            set debug remote 0
            printf "Remote debugging verbosity disabled.\n"
        end
    end
end

verbose_remote 0

# === Options for slow connections (RTL sims, etc.) ===

# Timeout for remote operations (in seconds)
set remotetimeout 300

# Enable caching of remote target memory and registers (speeds up GDB operations)
set remotecache on

# === General UI and printing options ===
set pagination off
set confirm off
set print pretty on
set print asm-demangle on

# === Architecture (adjust if 64-bit) ===
set architecture riscv:rv32

# === Connect to the remote target (vortex instance) ===
target remote localhost:3333

# === Initialize VX Debug environment ===
vxgdb_initialize

